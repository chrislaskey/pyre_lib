defmodule Pyre.Flows.TaskTest do
  use ExUnit.Case, async: false

  alias Pyre.Flows.Task

  setup do
    tmp_dir = Path.join(System.tmp_dir!(), "pyre_task_test_#{System.unique_integer([:positive])}")
    features_dir = Path.join(tmp_dir, "priv/pyre/features")
    File.mkdir_p!(features_dir)
    on_exit(fn -> File.rm_rf!(tmp_dir) end)
    %{tmp_dir: tmp_dir}
  end

  defp with_cwd(dir, fun) do
    original = File.cwd!()
    File.cd!(dir)

    try do
      fun.()
    after
      File.cd!(original)
    end
  end

  test "runs to completion with single mock response", %{tmp_dir: tmp_dir} do
    Process.put(:mock_llm_responses, [
      "# Summary\n\nTask completed successfully."
    ])

    result =
      with_cwd(tmp_dir, fn ->
        Task.run("Add pagination to the products page",
          llm: Pyre.LLM.Mock,
          streaming: false,
          project_dir: tmp_dir
        )
      end)

    assert {:ok, state} = result
    assert state.phase == :complete
    assert state.generalist_output =~ "Summary"
  end

  test "dry run skips LLM calls", %{tmp_dir: tmp_dir} do
    result =
      with_cwd(tmp_dir, fn ->
        Task.run("Add pagination",
          llm: Pyre.LLM.Mock,
          streaming: false,
          dry_run: true,
          project_dir: tmp_dir
        )
      end)

    assert {:ok, state} = result
    assert state.phase == :complete
  end

  test "propagates error from a failing LLM", %{tmp_dir: tmp_dir} do
    defmodule FailingLLM do
      use Pyre.LLM
      def generate(_, _, _ \\ []), do: {:error, :llm_failure}
      def stream(_, _, _ \\ []), do: {:error, :llm_failure}
      def chat(_, _, _, _ \\ []), do: {:error, :llm_failure}
    end

    result =
      with_cwd(tmp_dir, fn ->
        Task.run("Add pagination",
          llm: FailingLLM,
          streaming: false,
          project_dir: tmp_dir,
          log_fn: fn _ -> :ok end
        )
      end)

    assert {:error, :llm_failure} = result
  end

  test "is not interactive by default" do
    assert Task.default_interactive_stages() == []
  end

  test "log_fn receives stage messages", %{tmp_dir: tmp_dir} do
    Process.put(:mock_llm_responses, [
      "Task completed."
    ])

    logs = Agent.start_link(fn -> [] end) |> elem(1)

    with_cwd(tmp_dir, fn ->
      Task.run("Add pagination",
        llm: Pyre.LLM.Mock,
        streaming: false,
        project_dir: tmp_dir,
        log_fn: fn msg -> Agent.update(logs, &(&1 ++ [msg])) end
      )
    end)

    log_messages = Agent.get(logs, & &1)
    assert Enum.any?(log_messages, &(&1 =~ "Run directory:"))
    assert Enum.any?(log_messages, &(&1 =~ "Stage: generalist"))

    Agent.stop(logs)
  end
end
